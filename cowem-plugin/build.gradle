/*
 * CoWeM plugin v1 build file
 * Steven J Zeil, Old Dominion Univ.
 */

buildscript {
    repositories {
        jcenter()
    }
//    dependencies {
//        classpath 'org.hidetake:gradle-ssh-plugin:2.2.0+'
//    }
}

plugins {
    id 'groovy'
    id 'ivy-publish'
    id 'java'
    id 'java-gradle-plugin'
    id 'project-report'
    id 'jacoco'
    id 'pmd'
}
group = 'edu.odu.cs.zeil'
version = '1.12'

gradlePlugin {
    plugins {
        CourseWebsite {
            id="edu.odu.cs.cowem.course"
            implementationClass = "edu.odu.cs.cowem.CourseWebsite"
        }
        Group {
            id="edu.odu.cs.cowem.group"
            implementationClass = "edu.odu.cs.cowem.Group"
        }
        Documents {
            id="edu.odu.cs.cowem.documents"
            implementationClass = "edu.odu.cs.cowem.Documents"
        }
    }
}


// Credentials are loaded from ~/.gradle/gradle.properties
if(project.hasProperty("ivyRepoUser")){
    ext.ivyRepoUser = "$ivyRepoUser";
} else {
    ext.ivyRepoUser = "user";
}
if(project.hasProperty("ivyRepoPass")){
    ext.ivyRepoPass = "$ivyRepoPass";
} else {
    ext.ivyRepoPass = "password";
}


repositories {
    jcenter()
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }

    // Use my own CS dept repo
    ivy {
        url 'https://secweb.cs.odu.edu/~zeil/ivyrepo'
    }
}


publishing {
    publications {
        ivyJava(IvyPublication) {
            organisation 'edu.odu.cs.zeil'
            module 'cowem-plugin'
            revision project.version
            descriptor.status = 'integration'   // milestone, release
            descriptor.branch = 'v1'

            from components.java
        }
    }

    repositories {
        ivy {
            name 'ivyRepo'
            url 'sftp://atria.cs.odu.edu:22/home/zeil/secure_html/ivyrepo'
            // Readable via https://www.cs.odu.edu/~zeil/ivyrepo
            credentials {
                username project.ivyRepoUser
                password project.ivyRepoPass
            }
        }
    }
}



dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.hidetake:gradle-ssh-plugin:2.10.1+'
    compile 'org.pegdown:pegdown:1.6.0+'
    compile 'org.slf4j:slf4j-api:1.7.30+'
    compile 'net.sf.saxon:Saxon-HE:9.9.1-6+'
    compile 'org.eclipse.jgit:org.eclipse.jgit:5.6.0.201912101111-r+'
    compile 'com.moowork.gradle:gradle-node-plugin:1.3.1'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3+'
    testRuntime 'edu.odu.cs.zeil:report_accumulator:1.3+'
    testRuntime 'org.slf4j:slf4j-simple:1.7.30+'
}

sourceSets {
    test {
        java {
            srcDir 'src/integrationTest/java'
        }
    }
}


jar {
    manifest {
        attributes 'Implementation-Title': 'Course Website Management',
            'Implementation-Version': '0.1'
    }
    // from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}


////////  Website report Generation ///////////////////

/*
checkstyle {
    ignoreFailures = true
    showViolations = false
    configFile = rootProject.file("config/checkstyle/checkstyle.xml")
    //configProperties = [ "suppressionFile" : file('config/checkstyle/checkstyle.suppressions.xml')]
}

tasks.withType(Checkstyle) {
    reports {
        html.destination project.file("build/reports/checkstyle/main.html")
    }
}
*/



pmd {
    ruleSetFiles = files("config/pmd/pmd.xml")
    ignoreFailures = true
    consoleOutput = false
}

test.ignoreFailures=true
//checkstyleTest.enabled = false
pmdTest.enabled = false
check.dependsOn htmlDependencyReport

task copyWebPages (type: Copy) {
    from 'src/main/html'
    into 'build/reports'
}

task copyDocs (type: Copy) {
    from 'build/docs'
    into 'build/reports/docs'
    dependsOn 'javadoc'
}

task reports (dependsOn: ['htmlDependencyReport', 'javadoc', 'check',
    'copyWebPages', 'copyDocs', 'jacocoTestReport']) {
    description 'Generate all reports for this project'
    group 'Reporting'
}

def theBuildNumber = '';
if (project.hasProperty('buildNumber')) {
    theBuildNumber = project.buildNumber
}

def websiteDir = 'cowem-plugin'

task reportStats (type: JavaExec) {
    description 'Compute current project statistics from reports'
    group 'Reporting'
    dependsOn 'reports'
    classpath = sourceSets.test.runtimeClasspath
    main = 'edu.odu.cs.zeil.report_accumulator.Main';
    args = [theBuildNumber,
        'https://www.cs.odu.edu/~zeil/gitlab/' + websiteDir + '/reports/',
        'build/reports']
}

task deployReports (type: Copy) {
    description 'copy all reports to project web page'
    group 'Publishing'
    dependsOn 'reportStats'
    from 'build/reports'
    into '/home/zeil/secure_html/gitlab/' + websiteDir + '/reports'
}
