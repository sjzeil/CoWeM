/*
 * CoWeM plugin v1 build file
 * Steven J Zeil, Old Dominion Univ.
 */


plugins {
    id 'groovy'
    id 'java'
    id 'java-gradle-plugin'
    id 'project-report'
    id 'jacoco'
    id 'pmd'
    //id "org.xbib.gradle.plugin.git" version "2.2.0"
    //id 'signing'
    id 'maven-publish'
}

group 'edu.odu.cs'

sourceCompatibility = "1.17"
targetCompatibility = "1.17"


gradlePlugin {
    plugins {
        CourseWebsite {
            id="edu.odu.cs.cowem.course"
            implementationClass = "edu.odu.cs.cowem.CourseWebsite"
        }
        Group {
            id="edu.odu.cs.cowem.group"
            implementationClass = "edu.odu.cs.cowem.Group"
        }
        Documents {
            id="edu.odu.cs.cowem.documents"
            implementationClass = "edu.odu.cs.cowem.Documents"
        }
    }
}


// Credentials are loaded from ~/.gradle/gradle.properties
if(project.hasProperty("ivyRepoUser")){
    ext.ivyRepoUser = "$ivyRepoUser";
} else {
    ext.ivyRepoUser = "user";
}
if(project.hasProperty("ivyRepoPass")){
    ext.ivyRepoPass = "$ivyRepoPass";
} else {
    ext.ivyRepoPass = "password";
}


repositories {
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    maven {
        url 'https://github.com/sjzeil/mvnrepo/raw/main'
    }
}

/*
artifacts {
    archives javadocJar, sourcesJar
}
*/

/*signing {
    sign configurations.archives
}*/


dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation 'org.hidetake:gradle-ssh-plugin:2.10.1'
    implementation 'org.pegdown:pegdown:1.6.0'
    implementation 'org.slf4j:slf4j-api:2.0.6'
    implementation 'net.sf.saxon:Saxon-HE:11.4'
    implementation 'org.eclipse.jgit:org.eclipse.jgit:6.4.0.202211300538-r'
	
	testImplementation "org.junit.jupiter:junit-jupiter-api:5.13.0"
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.13.0'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.13.0' 
    testImplementation 'org.hamcrest:hamcrest-library:3.0'
//    testRuntimeOnly 'edu.odu.cs.zeil:report_accumulator:1.3+'
    testRuntimeOnly 'org.slf4j:slf4j-simple:2.0.6'
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}
configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test
}

check.dependsOn integrationTest


tasks.withType(Test) {
    useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
		}
}

check.dependsOn integrationTest


jar {
    manifest {
        attributes 'Implementation-Title': 'Course Website Management',
            'Implementation-Version': '0.1'
    }
    // from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}


//task install (dependsOn: ['jar', 'publishToMavenLocal']) {
//}

publishing {
    publications {
        cowemLibrary(MavenPublication) {
            from components.java
            groupId = 'edu.odu.cs'
            artifactId = project.name
            version = project.version
            pom {
                name = "cowem-plugin"
                description = 'Static generator for course websites'
                url = 'https://github.com/sjzeil/CoWeM'
                licenses {
                    license {
                        name = "MIT License"
                        url = "https://github.com/sjzeil/CoWeM/blob/main/LICENSE.txt"
                    }
                }
                developers {
                    developer {
                        id = 'sjzeil'
                        name = 'Steven Zeil'
                        email = 'szeil@odu.edu'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = 'Zeil-maven-repo'
            url = '../../mvnrepo'
        }
    }
}



////////  Website report Generation ///////////////////




pmd {
    ruleSetFiles = files("config/pmd/pmd.xml")
    ignoreFailures = true
    consoleOutput = false
}

test.ignoreFailures=true
pmdTest.enabled = false
check.dependsOn htmlDependencyReport

task copyWebPages (type: Copy) {
    from 'src/main/html'
    into 'build/reports'
}

task copyDocs (type: Copy) {
    from 'build/docs'
    into 'build/reports/docs'
    dependsOn 'javadoc'
}

task reports (dependsOn: ['htmlDependencyReport', 'javadoc', 'check',
    'copyWebPages', 'copyDocs', 'jacocoTestReport']) {
    description 'Generate all reports for this project'
    group 'Reporting'
}

/*
def theBuildNumber = '';
if (project.hasProperty('buildNumber')) {
    theBuildNumber = project.buildNumber
}

def websiteDir = 'cowem-plugin'

task reportStats (type: JavaExec) {
    description 'Compute current project statistics from reports'
    group 'Reporting'
    dependsOn 'reports'
    classpath = sourceSets.test.runtimeClasspath
    main = 'edu.odu.cs.zeil.report_accumulator.Main';
    args = [theBuildNumber,
        'https://www.cs.odu.edu/~zeil/gitlab/' + websiteDir + '/reports/',
        'build/reports']
}

task deployReports (type: Copy) {
    description 'copy all reports to project web page'
    group 'Publishing'
    dependsOn 'reportStats'
    from 'build/reports'
    into '/home/zeil/secure_html/gitlab/' + websiteDir + '/reports'
}

*/

////////  Website publication on GitHub pages ///////////////////


task clonePages() {
    doLast {
        mkdir 'build/gh-pages'
        def thisRepo = rootProject.projectDir.toString()
        def pagesDir = "$buildDir/gh-pages"
        project.delete {
            delete pagesDir
        }
        def grgit = git.clone {
            dir = pagesDir
            uri = 'file:' + thisRepo
            bare = false
            refToCheckout = 'gh-pages'
        }
        grgit.checkout {
            branch = 'gh-pages'
        }
    }
}

task copyReports (type: Copy, dependsOn: ['reports', 'clonePages']) {
    from "$buildDir/reports"
    into "$buildDir/gh-pages/reports"
}

task updateGHPages (dependsOn: 'copyReports') {
    group = "Reporting"
    description  'Copies reports to the gh-pages branch in preparation for a future push to GitHub'
    doLast {
        exec {
            workingDir "$buildDir/gh-pages/reports"
            commandLine '/bin/sh', '-c', 'date > date.txt'
        }
        def pagesDir = "$buildDir/gh-pages"
        def grgit = git.open {
            dir = pagesDir + "/.git"
        }
        grgit.add (update: false, patterns: ['reports/'])
        grgit.add (update: true, patterns: ['reports/'])
        grgit.commit {
            message = "Updating web pages"
        }
        grgit.push {}
    }
}
