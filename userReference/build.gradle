/*
 * CWM course website v1 build file
 * Steven J Zeil, Old Dominion Univ.
 */

project.ext.deployDestination = file('build/websiteFakeDeploy/')


allprojects {

    repositories {
        // jcenter()

        mavenCentral()

    
        // Use my own CS dept repo
        ivy {
            url 'https://secweb.cs.odu.edu/~zeil/ivyrepo'
        }
    }
}


configurations {
    setup
    build
    deploy
}

dependencies {
    setup 'edu.odu.cs.zeil:cwm-core:latest.integration'
    build 'edu.odu.cs.zeil:cwm-utils:1.0-SNAPSHOT'
}

/*
 * Tasks for course website construction:
 *
 * setup: copies core files & prepares directories as necessary
 *
 * build: collects document metadata
 *        processes each document set
 *        processes course outline
 *
 * packages: (optional) prepare course cartridges
 *              available formats are imscc, bb, bbthin, canvas, canvasthin, epub, mobi
 *
 * deploy: (optional) copy website to local destination
 *
 * publish: (optional) copy website to remote destination (via rsync or scp)
 *
 * -----------------------------------------------------------------------------
 * Directory structure
 * 
 * course root/
 * |
 * |- course.properties
 * |
 * |- Directory/    website navigation pages
 * |--|
 * |--|- outline/   course topics/outline description (required)
 * |--|- nav/       navigation bar shared by all Directory pages
 * |--|- .../       Other navigational document sets, e.g., policies/, library/, grades/
 * |
 * |- Public/    public document sets
 * |--|
 * |--|- syllabus/ course syllabus
 * |--|- .../      other public documetn sets (typically, slides & lecture notes)
 * |
 * |- Protected/  document sets restricted to certain readers
 * |--|- Assts/     assignments
 * |--|- .../       other protected document sets (e.g., semester projects)
 * |
 * |--|- graphics/  Course-specific files that override defaults graphics used for
 * |                  navigation, callouts, backgrounds, etc.  
 * |--|- styles/  Course-specific overrides for .css and .js defaults
 * |
 * |- build/ All automatically constructed content goes here. This entire directory
 * |--|      may be deleted without loss of anything but time.   
 * |--|- cwm/   Copies of CWM core files
 * |--|--|
 * |--|--|- graphics/  Default graphics for navigation, callouts, backgrounds, etc.
 * |--|--|
 * |--|--|- styles/    Default css & js files
 * |--|--|
 * |--|--|- templates/ XSL and other templates used to process web content
 * |--|         
 * |--|- website/  generated website
 * |--|--|
 * |--|--|- index.html
 * |--|--|- Directory/  website navigation pages
 * |--|--|- Public/     public document sets
 * |--|--|- Protected/  document sets restricted to certain readers
 * |--|--|- graphics/    
 * |--|--|- styles/
 * |--|         
 * |--|- cartridges/  generated cartridges
 */
 
 task setup_cwm (type: Copy) {
     dependsOn configurations.setup
     into file('build/cwm')     
     from ({ zipTree(configurations.setup.singleFile) }) {  
         include 'edu/odu/cs/cwm/core/templates/**'
         include 'edu/odu/cs/cwm/core/graphics/**'
         include 'edu/odu/cs/cwm/core/styles/**'
     }
 }

 setup_cwm << {
    ant.move (file: "${projectDir}/build/cwm/edu/odu/cs/cwm/core/templates", 
              tofile: "${projectDir}/build/cwm/templates") 
 }

 
 task setup_copy_website_defaults (type: Copy) {
     from 'build/cwm/edu/odu/cs/cwm/core'
     into 'build/website'
     include 'graphics/**'
     include 'styles/**'
 }  
 
 task setup_copy_website_overrides (dependsOn: setup_copy_website_defaults) << {
     copy  {
         from 'graphics'
         into 'build/website/graphics'
     }
     copy  {
         from 'styles'
         into 'build/website/styles'
     }
 }  
 
 task setup_website (dependsOn: 'setup_copy_website_overrides')


 task setup {
     description 'Prepare output and support directories'
     dependsOn setup_cwm, setup_website
 }
 
 
 task build(dependsOn: 'setup') {
     description 'Process documents and course outline to prepare the basic course website.'

 }
 
 
 task packages(dependsOn: 'build')  {
     description 'prepare optional course cartridges'
 }
 
 
 
 task deploy(type: Sync, dependsOn: 'build') {
     from 'build/website'
     into rootProject.deployDestination
     dirMode 0775
     includeEmptyDirs true   
 }
 
 task publish(dependsOn: 'build') {
 
 }
 
 